import("//brave/build/config.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//testing/test.gni")
import("//tools/grit/preprocess_if_expr.gni")

preprocess_folder = "preprocessed"
preprocess_mojo_manifest = "preprocessed_mojo_manifest.json"

static_library("common") {
  sources = [
    "eth_request_helper.cc",
    "eth_request_helper.h",
    "features.cc",
    "features.h",
    "hex_utils.cc",
    "hex_utils.h",
    "value_conversion_utils.cc",
    "value_conversion_utils.h",
  ]
  deps = [
    ":common_constants",
    ":mojom__generator",
    "//base",
  ]
}

source_set("common_constants") {
  sources = [
    "web3_provider_constants.cc",
    "web3_provider_constants.h",
  ]
  deps = [ ":mojom" ]
}

preprocess_if_expr("preprocess_mojo") {
  deps = [ "//brave/components/brave_wallet/common:mojom_js" ]
  in_folder = "$target_gen_dir"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_mojo_manifest"
  in_files = [ "brave_wallet.mojom-lite.js" ]
}

mojom("mojom") {
  generate_java = true
  sources = [ "brave_wallet.mojom" ]

  deps = [
    "//mojo/public/mojom/base",
    "//url/mojom:url_mojom_gurl",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "eth_request_helper_unittest.cc",
    "hex_utils_unittest.cc",
    "value_conversion_utils_unittest.cc",
  ]

  deps = [
    ":common",
    ":mojom",
    "//base/test:test_support",
    "//testing/gtest",
    "//url",
  ]
}  # source_set("brave_wallet_common_unit_tests")
